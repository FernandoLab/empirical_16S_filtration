REFERENCE_LOCATION=/Volumes/Wes_External/avo_git/silva.nr_v138/
ALIGNREF=silva.v4.fasta
TAXONREF_FASTA=silva.nr_v138.align
TAXONREF_TAX=silva.nr_v138.tax
CONTAMINENTS=Chloroplast-Mitochondria-unknown-Archaea-Eukaryota
LOGNAME=dairy_mothur
DATA=/Volumes/Wes_External/avo_git/negative_control
TYPE=fastq
PROC=8
#batch commands
set.logfile(name=$LOGNAME)
make.file(inputdir=$DATA, type=$TYPE, prefix=nc)
make.contigs(file=current, processors=$PROC)
summary.seqs(fasta=current, count=current)
screen.seqs(fasta=current, count=current, maxambig=0, maxlength=275, maxhomop=8)
get.current()
summary.seqs(fasta=current,count=current)
unique.seqs(fasta=current, count=current)
summary.seqs(count=current)
align.seqs(fasta=current, reference=$REFERENCE_LOCATION/$ALIGNREF)
summary.seqs(fasta=current, count=current)
screen.seqs(fasta=current, count=current, start=1968, end=11550)
summary.seqs(fasta=current, count=current)
filter.seqs(fasta=current, vertical=T, trump=.)
unique.seqs(fasta=current, count=current)
pre.cluster(fasta=current, count=current, diffs=2)
chimera.vsearch(fasta=current, count=current, dereplicate=t)
summary.seqs(fasta=current, count=current)
classify.seqs(fasta=current, count=current, reference=$REFERENCE_LOCATION/$TAXONREF_FASTA, taxonomy=$REFERENCE_LOCATION/$TAXONREF_TAX, cutoff=80)
remove.lineage(fasta=current, count=current, taxonomy=current, taxon=$CONTAMINENTS)
summary.tax(taxonomy=current, count=current)
rename.file(fasta=current, count=current, taxonomy=current, prefix=final)
cluster.split(fasta=current, count=current, taxonomy=final.taxonomy, taxlevel=4, cutoff=0.03)
make.shared(list=current, count=current, label=0.03)
classify.otu(list=current, count=current, taxonomy=current, label=0.03)
#phylotype(taxonomy=current)
#make.shared(list=current, count=current, label=1)
#classify.otu(list=current, count=current, taxonomy=current, label=1)
